buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPlatformVersion}")
        classpath("se.transmode.gradle:gradle-docker:${dockerPluginVersion}")
    }
}

plugins {
    id 'java'
    id 'idea'
}
apply plugin: 'docker'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-allopen'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = javaVersion
compileKotlin {
    kotlinOptions.jvmTarget = javaVersion
}
compileTestKotlin {
    kotlinOptions.jvmTarget = javaVersion
}

dependencies {
    compile ("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
    compile ("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    compile ('org.eclipse.jgit:org.eclipse.jgit:4.5.2.201704071617-r')

    compile ('org.springframework.boot:spring-boot-starter-web')
    compile ('org.springframework.boot:spring-boot-starter-actuator')
    compile ('org.springframework.boot:spring-boot-starter-thymeleaf')

    testCompile ('org.mockito:mockito-core:2.7.22')
    testCompile ('com.nhaarman:mockito-kotlin:1.4.0') { transitive = false }
    testCompile ('info.novatec.testit:testutils-mockito:0.2')
    testCompile ('org.assertj:assertj-core:3.7.0')

    testCompile ("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile ("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")

    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    tag = 'caaqe/asset-overview-spring'

    baseImage = 'frolvlad/alpine-oraclejdk8:slim'
    volume("/tmp")
    addFile {
        from bootJar
        rename { 'app.jar' }
    }
    entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
}
